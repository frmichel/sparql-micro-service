CONSTRUCT {
    <http://example.org/ld/taxon/{urlencode(name)}>
        a dwc:Taxon;
        schema:image ?photoUri;

        # Reinject the service argument into the graph
        dwc:scientificName {name}.


    ?photoUri a schema:Photograph;

        schema:name ?title;
        schema:contentUrl ?img; foaf:depiction ?img;
        schema:thumbnailUrl ?thumbnail;

        schema:mainEntityOfPage ?pageUrl;
        schema:fileFormat "image/jpeg";
        schema:dateCreated ?dateTaken;

        schema:author [ 
            a schema:Person;
            schema:name ?ownerName; schema:identifier ?owner; schema:url ?ownerUrl
        ];
        .
}
WHERE {
    ?photo 
        api:id ?id;
        api:title ?title;
        api:owner ?owner;
        api:secret ?secret; api:server ?server; api:farm ?farm.

    OPTIONAL { ?photo api:ownername ?ownerName }
    OPTIONAL { ?photo api:datetaken ?dateTaken }

    # Dereferenceable URI: customize with the hostname of your Web server
    bind (IRI(concat("http://example.org/ld/flickr/photo/", ?id)) AS ?photoUri)

    bind (IRI(concat("https://flickr.com/photos/", ?owner, "/", ?id)) AS ?pageUrl)
    bind (IRI(concat("https://flickr.com/photos/", ?owner)) AS ?ownerUrl)
    bind (IRI(concat("https://farm", ?farm, ".staticflickr.com/", ?server, "/", ?id, "_", ?secret, "_z.jpg")) AS ?img)
    bind (IRI(concat("https://farm", ?farm, ".staticflickr.com/", ?server, "/", ?id, "_", ?secret, "_s.jpg")) AS ?thumbnail)
}