prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
prefix schema: <http://schema.org/>
prefix taxrefprop: <http://taxref.mnhn.fr/lod/property/>
prefix taxrefrk: <http://taxref.mnhn.fr/lod/taxrank/>
prefix dwc: <http://rs.tdwg.org/dwc/terms/>
prefix dwciri: <http://rs.tdwg.org/dwc/iri/>
prefix dct: <http://purl.org/dc/terms/>
prefix sms: <http://sms.i3s.unice.fr/schema/>

CONSTRUCT {
    ?taxon a owl:Class;
        rdfs:label ?taxonName;              # TAXREF-LD
        schema:image ?photo;                # Flickr
        schema:audio ?audio;                # Macauley Lib
        schema:subjectOf ?mbzPage;          # Musicbrainz
        schema:subjectOf ?article;          # BHL
        dct:relation ?measure.              # EoL

    ?photo                                  # Flickr
        schema:contentUrl ?img; schema:thumbnailUrl ?imgThumbnail; schema:author ?imgAuthor.
    ?imgAuthor schema:name ?imgAuthorName; schema:url ?imgAuthorUrl.

    ?audio                                  # Macauley Lib
        schema:contentUrl ?audioUrl; schema:thumbnailUrl ?audioThumbnail;
        schema:mainEntityOfPage ?audioPage; schema:author ?audioAuthor; schema:description ?audioDesc.

    ?article a schema:ScholarlyArticle;     # BHL
        schema:about ?taxon;
        schema:author ?articleAuthorName;
        schema:mainEntityOfPage ?articlePage;
        schema:thumbnailUrl ?articleThumbnailUrl;
        schema:description ?articleCitation.

    ?measure a dwc:MeasurementOrFact;       # EoL
        dwc:measurementType ?measurementType;
        dwc:measurementUnit ?measurementUnit;
        dwciri:measurementUnit ?measurementUnitUri;
        dwc:measurementValue ?measurementValue;
    .

} WHERE {
    # First, retrieve the taxon name from the URL query string (?param=taxon+name)
    BIND(st:get(st:param) as ?param)
    BIND(if (bound(?param) && ?param != "", ?param, "") as ?taxonName)

    SERVICE <http://taxref.mnhn.fr/sparql> {
        ?taxon a owl:Class;
            rdfs:label ?taxonName.
        FILTER (STRENDS(STR(?taxon), "/10.0"))
    }

    BIND(sms:flickrService("806927@N20", ?taxonName) as ?flickr)
    OPTIONAL {
        SERVICE SILENT ?flickr
        { SELECT * WHERE {
            ?photo schema:contentUrl ?img; schema:thumbnailUrl ?imgThumbnail; schema:author ?imgAuthor.
            ?imgAuthor schema:name ?imgAuthorName; schema:url ?imgAuthorUrl.
        } LIMIT 10 }
    }

    OPTIONAL {
        SERVICE SILENT <http://sms.i3s.unice.fr/sparql-ms/macaulaylibrary/getAudioByTaxon_sd>
        { SELECT * WHERE {
            []  a dwc:Taxon;
                dwc:scientificName ?taxonName;
                schema:audio ?audio.
                
            ?audio
                schema:contentUrl ?audioUrl; schema:thumbnailUrl ?audioThumbnail;
                schema:mainEntityOfPage ?audioPage; schema:author ?audioAuthor; schema:description ?audioDesc.
          } limit 6
        }
    }

    BIND(sms:musicBrainzService(?taxonName) as ?musicbrainz)
    OPTIONAL {
        SERVICE SILENT ?musicbrainz
        { [] schema:sameAs ?mbzPage; schema:name ?mbzTitle. }
    }

    BIND(sms:bhlService(?taxonName) as ?bhl)
    OPTIONAL {
        SERVICE SILENT ?bhl
        { SELECT * WHERE {
            ?article
                schema:name ?articleTitle;
                schema:thumbnailUrl ?articleThumbnailUrl;
                schema:author ?articleAuthorName;
                schema:mainEntityOfPage ?articlePage;
                schema:isPartOf [
                    schema:name ?articleContainerTitle;
                    schema:datePublished ?articleDate;
                ].
          } LIMIT 6
        }
        BIND( CONCAT(?articleTitle, ". ", ?articleContainerTitle, " (", ?articleDate, ").") AS ?articleCitation )
    }

    OPTIONAL {
        SERVICE SILENT <http://sms.i3s.unice.fr/sparql-ms/eol/getTraitsByTaxon_sd>
        { SELECT * WHERE {
            []  a dwc:Taxon;
                dwc:scientificName ?taxonName;
                dct:relation ?measure.

            ?measure a dwc:MeasurementOrFact;
                dwc:measurementType ?measurementType;
                dwc:measurementValue ?measurementValue.
            OPTIONAL { ?measure dwc:measurementUnit ?measurementUnit }
            OPTIONAL { ?measure dwciri:measurementUnit ?measurementUnitUri }
            
            FILTER (?measurementType  != 'habitat includes' && ! strStarts(?measurementType, "water"))
            
          } LIMIT 20
        }
    }
}


function sms:flickrService(?group_id, ?biname) {
    IRI(concat("http://sms.i3s.unice.fr/sparql-ms/flickr/getPhotosByGroupByTag?",
        "group_id=", encode_for_uri(?group_id),
        "&tags=", encode_for_uri(concat("taxonomy:binomial=", ?biname))))
}
function sms:musicBrainzService(?name) {
    IRI(concat("http://sms.i3s.unice.fr/sparql-ms/musicbrainz/getSongByName?name=", encode_for_uri(?name)))
}
function sms:bhlService(?name) {
    IRI(concat("http://sms.i3s.unice.fr/sparql-ms/bhl/getArticlesByTaxon?name=", encode_for_uri(?name)))
}
